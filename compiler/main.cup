import "std/file.cup"
// FIXME: The order of the following lines is important.
//        Perhaps have multiple passes?
import "compiler/builtins.cup"
import "compiler/parser.cup"
import "compiler/codegen.cup"

// FIXME: Allow variables functions to be static to a file?
//        we can avoid the prefixes then.
const MAX_STDIN_SOURCE_LEN = 4096;
let m_filename: char*;
let m_output_filename = "build/output";
let m_source: char*;
let m_source_len = 0;
let m_dump_ast = false;
let m_run_exec = false;
let m_silent = false;

fn print_usage_and_exit(name: char*, status: int) {
    puts("Usage: "); puts(name); putsln(" [options] <file>");
    putsln("Options:");
    putsln("  -c <code>  Code to compile");
    putsln("  -h         Show this help");
    putsln("  -s         Silence debug output");
    putsln("  -r         Run the generated executable");
    putsln("  -o <file>  Output file");
    putsln("  -d         Dump AST to stdout");
    putsln("Compilation options:");
    putsln("  --alloc    Change default allocator");
    puts("Output file will be named '"); puts(m_output_filename); putsln("' by default");
    exit(status);
}

fn parse_cli_args(argc: int, argv: char**): char** {
    for (let i = 1; i < argc; ++i) {
        if (streq(argv[i], "-c")) {
            m_source_len = strlen(argv[i+1]);
            m_source = argv[i+1];
            ++i;
            m_filename = "CLI";
        } else if (streq(argv[i], "-s")) {
            m_silent = true;
        } else if (streq(argv[i], "-r")) {
            m_run_exec = true;
        } else if (streq(argv[i], "-h")) {
            print_usage_and_exit(argv[0], 0);
        } else if (streq(argv[i], "-d")) {
            m_dump_ast = true;
        } else if (streq(argv[i], "-o")) {
            ++i;
            m_output_filename = argv[i];
        } else if (streq(argv[i], "--alloc")){
            ++i;
            p_default_allocator = malloc(strlen(argv[i])*sizeof(char));
            strcpy(p_default_allocator, argv[i]);
        } else if (m_filename == null) {
            if (streq(argv[i], "-")) {
                m_filename = "stdin";
                m_source = malloc(MAX_STDIN_SOURCE_LEN);
                m_source_len = read(0, m_source, MAX_STDIN_SOURCE_LEN);
                m_source[m_source_len] = 0;
                if (m_source_len == MAX_STDIN_SOURCE_LEN) {
                    putsln("Source too long to use through stdin");
                    exit(1);
                }
            } else {
                m_filename = argv[i];
                let file = fopen(m_filename, 'r');
                m_source = file::slurp(&m_source_len);
                file::close();
            }
        } else if (!m_run_exec) {
            // No `-r` option passed, we shouldn't have any extra args.
            print_usage_and_exit(argv[0], 1);
        } else {
            // Any extra args with `-r` are passed to the executable.
            return argv + i;
        }
    }
    if (m_filename == null) {
        print_usage_and_exit(argv[0], 1);
    }
    return argv + argc;
}

fn main(argc: int, argv: char **, envp: char**): int {
    let subproc_args = parse_cli_args(argc, argv);

    let lexer = lexer_new(m_filename, m_source, m_source_len);
    let ast = parse_program(lexer);

    if (m_dump_ast)
        dump_ast(ast, 0);

    let asm_filename: char[256];
    strcpy(asm_filename, m_output_filename);
    replace_extension(asm_filename, ".yasm");

    let out_file = fopen(asm_filename, 'w');
    generate_program(ast, out_file);
    out_file::close();   // Want to flush before we assemble.

    let obj_filename: char[256];
    strcpy(obj_filename, m_output_filename);
    replace_extension(obj_filename, ".o");

    let cmd_args: char*[10];
    cmd_args[0] = "/usr/bin/env";
    cmd_args[1] = "yasm";
    cmd_args[2] = OS_IS_MACOS ? "-fmacho64" : "-felf64";
    cmd_args[3] = "-o";
    cmd_args[4] = obj_filename;
    cmd_args[5] = asm_filename;
    cmd_args[6] = null;
    run_command_env(cmd_args, envp, !m_silent);

    // cmd_args[0] is already /usr/bin/env
    cmd_args[1] = OS_IS_MACOS ? "clang" : "ld";
    cmd_args[2] = "-o";
    cmd_args[3] = m_output_filename;
    cmd_args[4] = obj_filename;
    if (OS_IS_MACOS)
        cmd_args[5] = "-Wl,-no_pie";
    else
        cmd_args[5] = null;
    cmd_args[6] = null;
    run_command_env(cmd_args, envp, !m_silent);

    if (m_run_exec) {
        --subproc_args; // Always valid
        subproc_args[0] = m_output_filename;
        run_command_env(subproc_args, envp, !m_silent);
    }

    // puts("---------------------------\n");
    // puts("Total amount of memory used by malloc: ");
    // putu(__malloc_buf_pos);
    // putsln("\nDone.");
}